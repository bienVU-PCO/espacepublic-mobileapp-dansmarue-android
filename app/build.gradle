buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.1'
        classpath 'com.facebook.testing.screenshot:plugin:0.10.0'
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

plugins {
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.facebook.testing.screenshot'
apply plugin: 'jacoco'
//apply plugin: 'com.jakewharton.butterknife'



ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 9
ext.versionCode = 7
ext.minimumSdkVersion = 21

android {
    // target Android 13
    compileSdkVersion 33
    // Build Tools
    buildToolsVersion '30.0.3'

    flavorDimensions "versionCode"

    def releaseNotes = 'git log --pretty=format:"%s" --since "yesterday"'.execute([], project.rootDir).text.trim()

    defaultConfig {
        applicationId "fr.pc.android.bienvu"
        minSdkVersion 21
        // target Android 12
        targetSdkVersion 33
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resValue "string", "PATH_PICTURES", 'Android/data/' + applicationId + '/files/Pictures'
        manifestPlaceholders = [MAPS_API_KEY: 'AIzaSyCj24XSmogs9303sAZgpBrU93aw6ftWnm8']
    }

    // Keep the following configuration in order to target Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

	productFlavors {

        dev {
            buildConfigField "String", "SIRA_API_URL", '"http://api-back-office/rest/"'
            buildConfigField "String", "SIRA_API_URL_MOCK", '"http://api-back-office-mock/rest/"'
            buildConfigField "String", "API_URL_EQUIPEMENT", '"http://api-back-office/rest/"'
            buildConfigField "String", "AUTHENT_API_URL", '"https://authen-api/json"'
            buildConfigField "String", "AUTHENT_HEADER", '"mcpAuth"'
            applicationId "fr.pc.android.bienvu"
            resValue "string", "app_name", "bienVU (Dev)"
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher",
                    appLabel: "bienVU (Dev)"
            ]
        }

        prec {
            buildConfigField "String", "SIRA_API_URL", '"http://api-back-office/rest/"'
            buildConfigField "String", "SIRA_API_URL_MOCK", '"http://api-back-office-mock/rest/"'
            buildConfigField "String", "API_URL_EQUIPEMENT", '"http://api-back-office/rest/"'
            buildConfigField "String", "AUTHENT_API_URL", '"https://authen-api/json"'
            buildConfigField "String", "AUTHENT_HEADER", '"mcpAuth"'
            applicationId "fr.pc.android.bienvu"
            resValue "string", "app_name", "bienVU (Prec)"
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher",
                    appLabel: "bienVU (Prec)"
            ]
        }

        rec {

//            o	R57 : https://teleservices.rec.apps.paris.fr/ramen/rest/
//            o	R59 : https://teleservices.rec.apps.paris.fr/signalement/rest/
//            o	R60 : https://teleservices.rec.apps.paris.fr/equipement/rest/

            buildConfigField "String", "SIRA_API_URL", '"http://api-back-office/rest/"'
            buildConfigField "String", "SIRA_API_URL_MOCK", '"http://api-back-office-mock/rest/"'
            buildConfigField "String", "API_URL_EQUIPEMENT", '"http://api-back-office/rest/"'
            buildConfigField "String", "AUTHENT_API_URL", '"https://authen-api/json"'
            buildConfigField "String", "AUTHENT_HEADER", '"mcpAuth"'
            applicationId "fr.pc.android.bienvu"
            resValue "string", "app_name", "bienVU (Rec)"
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher",
                    appLabel: "bienVU (Rec)"
            ]
        }

        prod {
            buildConfigField "String", "SIRA_API_URL", '"http://api-back-office/rest/"'
            buildConfigField "String", "SIRA_API_URL_MOCK", '"http://api-back-office-mock/rest/"'
            buildConfigField "String", "API_URL_EQUIPEMENT", '"http://api-back-office/rest/"'
            buildConfigField "String", "AUTHENT_API_URL", '"https://authen-api/json"'
            buildConfigField "String", "AUTHENT_HEADER", '"mcpAuth"'
            applicationId "com.plainecommune.bienvu"
            resValue "string", "app_name", "bienVU"
            manifestPlaceholders = [
                    appIcon : "@mipmap/ic_launcher",
                    appLabel: "bienVU"
            ]

        }
    }

    signingConfigs {
        release
    }

    buildTypes {

        debug {
            ext.betaDistributionGroupAliases = "dansmarue-android-acn"
            ext.betaDistributionReleaseNotes = releaseNotes
            testCoverageEnabled = true
        }
        release {
            ext.betaDistributionGroupAliases = "dansmarue-android-acn"
            ext.betaDistributionReleaseNotes = releaseNotes

            minifyEnabled false
            testCoverageEnabled = false

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def propFilePath = null
    if (project.hasProperty("AndroidProject.signing")
            && new File(project.property("AndroidProject.signing").toString()).exists()) {
        propFilePath = project.property("AndroidProject.signing")
    } else if (System.getenv("SIGN_CONFIG") != null) {
        propFilePath = System.getenv("SIGN_CONFIG") as String
    }

    if (propFilePath != null) {
        def Properties props = new Properties()
        def propFile = new File(propFilePath)
        if (propFile.canRead()) {
            props.load(new FileInputStream(propFile))
            if (props != null && props.containsKey('STORE_PASSWORD') &&
                    props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                android.signingConfigs.release.storeFile = file("dmr.keystore")
                android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
                android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
                android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
            } else {
                println 'androidproject.properties found but some entries are missing'
                android.buildTypes.release.signingConfig = null
            }
        } else {
            println 'androidproject.properties file not found'
            android.buildTypes.release.signingConfig = null
        }
    } else {
        println 'AndroidProject.signing property not found. modify your local gradle.properties file as follow -> AndroidProject.signing=C:\\\\Android\\\\projets\\\\bienVU\\\\properties\\\\androidproject.properties'
        println 'androidproject.properties contains the STORE_PASSWORD, KEY_ALIAS and KEY_PASSWORD properties'
    }


    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    namespace 'com.accenture.dansmarue'
    lint {
        abortOnError false
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation fileTree(include: ['*.jar', '*.json'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation 'com.google.dagger:dagger:2.42'
    annotationProcessor "com.google.dagger:dagger-compiler:2.23.2"

    implementation 'com.github.bumptech.glide:glide:4.15.1'

    //noinspection AndroidLintGradleCompatible
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //noinspection GradleCompatible
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation 'com.mikhaellopez:circularimageview:3.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'org.apache.commons:commons-collections4:4.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.5'
    implementation 'androidx.mediarouter:mediarouter:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    implementation ('com.google.android.libraries.places:places:3.2.0'){
        exclude module: "glide"
    }

    implementation 'com.google.firebase:firebase-crashlytics:18.3.7'
    implementation 'com.google.firebase:firebase-analytics:21.3.0'


    //implementation ('com.google.android.libraries.places:places-compat:1.1.0') {
    //    exclude module: "glide"
    //}

    implementation 'com.google.firebase:firebase-messaging:23.1.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.0'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.1'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.6.1'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.1'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.1'
    androidTestImplementation 'org.mockito:mockito-core:3.12.0'
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'com.github.luizgrp:SectionedRecyclerViewAdapter:v3.2.0'

    //implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.5.0'

    // connecthings beacon notification
    //compile "com.connecthings.adtag:android-adtag-detection:3.1.10"
    //compile 'com.connecthings.adtag:android-adtag-beacon:3.0.3'

}

private Integer generateVersionCode() {
    return ext.versionCode
}

public String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

// Define coverage source.
// If you have rs/aidl etc... add them here.
def coverageSourceDirs = [
        'src/main/java',
]

task jacocoTestReport(type: JacocoReport, dependsOn: "test") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories.from = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories.from = files(coverageSourceDirs)
    executionData.from = fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

task jacocoUnitTestReport(type: JacocoReport, dependsOn: "test") {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['/di/**', '**/*_*', '**/*$*', '/ui/activities/**', '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/dev/debug/com/accenture/dansmarue").exclude(fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

task printVersion() {
    def file = new File("version.txt")
    file.createNewFile()
    file.text = generateVersionName()
}
